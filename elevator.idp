// First name: Bo
// Last name: Kleynen
// Student number: r0624034


// FIXED VOCABULARY
// DO NOT CHANGE THIS VOCABULARY
LTCvocabulary V_fixed {
    type Time isa nat			// A set of time points
    Start: Time 				// The initial time point
    partial Next(Time): Time 	// The successor time point of time point t
    
    type Floor isa nat									// The set of floors
    type DoorState constructed from {Closed, Open}		// A set of possible states of the elevator’s doors
	
    // Fluents
    ElPosition(Time): Floor				// The floor of the elevator at time point t
    ElDoorState(Time): DoorState		// The state of the elevator’s doors at time point t
    UnansweredRequest(Time, Floor)		// Denotes that there is an unanswered request at floor f at time point t
    
    // Actions
    MakeRequest(Time, Floor) 			// Denotes that a request is made for floor f at time point t
}


// STUDENT VOCABULARY
// Here you can add new types, functions and predicates
LTCvocabulary V_student {
    extern vocabulary V_fixed
    
    type Direction constructed from {Up, Down}
    
    type Action constructed from {Move, ChangeDoorState}
    
    // Fluens
    Direction(Time, Direction)
    
    
    // Actions
	do(Time, Action)
    Move(Time)
    ChangeDoorState(Time)
    CloseDoors(Time)
    OpenDoors(Time)
    
    // CanChangeDirection(t) is true iff can change direction.
    CanChangeDirection(Time)
    // MovingDown(t) is true iff the elevator is answering requests above ElPosition(t) at time point t.
    MovingDown(Time)
    // MovingUp(t) is true iff the elevator is answering requests above ElPosition(t) at time point t.
    MovingUp(Time)
    
    UnansweredRequestAbove(Time, Floor)
    UnansweredRequestBelow(Time, Floor)
    
    MakeRequestAbove(Time, Floor)
    MakeRequestBelow(Time, Floor)
    
    I_ElPosition(Floor)
    C_ElPosition(Time, Floor)
    Cn_ElPosition(Time, Floor)
    
    I_ElDoorState(DoorState)
    C_ElDoorState(Time, DoorState)
    Cn_ElDoorState(Time, DoorState)
    
    I_UnansweredRequest(Floor)
    C_UnansweredRequest(Time, Floor)
    Cn_UnansweredRequest(Time, Floor)
    
    // No need to specify an initial direction.
    C_Direction(Time, Direction)
    Cn_Direction(Time, Direction)
}

// TIME THEORY
// DO NOT CHANGE THIS THEORY
Theory T_fixed : V_student {
    // Defining time
    {
        Start = MIN[:Time].
        ! t: Next(t) = t+1 <- Time(t+1).
    }
}


// STUDENT THEORY
// Here you should model the dynamic elevator system
Theory T_student: V_student {    
    // Some syntactic sugar
    ! t[Time]: Move(t) <=> do(t, Move).
    ! t[Time]: ChangeDoorState(t) <=> do(t, ChangeDoorState).
    ! t[Time]: CloseDoors(t) <=> ChangeDoorState(t) & ElDoorState(t) = Open.
    ! t[Time]: OpenDoors(t) <=> ChangeDoorState(t) & ElDoorState(t) = Closed.
    ! t[Time]: MovingDown(t) <=> Direction(t, Down).
    ! t[Time]: MovingUp(t) <=> Direction(t, Up).
    ! t[Time] f[Floor]: UnansweredRequestAbove(t, f) <=> UnansweredRequest(t, f) & f > ElPosition(t).
    ! t[Time] f[Floor]: UnansweredRequestBelow(t, f) <=> UnansweredRequest(t, f) & f < ElPosition(t).
    ! t[Time] f[Floor]: MakeRequestAbove(t, f) <=> MakeRequest(t, f) & f > ElPosition(t).
    ! t[Time] f[Floor]: MakeRequestBelow(t, f) <=> MakeRequest(t, f) & f < ElPosition(t).
    
    // In the first time step the elevator is at the bottom floor
    I_ElPosition(MIN).
    
    // In the first time step the door is closed
    I_ElDoorState(Closed).
    
    // In the first time step there can be no unanswered requests.
    ! f[Floor]: ~I_UnansweredRequest(f).
  
    
    // ElPosition inertia.
    {
        ! f[Floor]: ElPosition(Start) = f <- I_ElPosition(f).
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- C_ElPosition(t, f).
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- ElPosition(t) = f & ~Cn_ElPosition(t, f).
    }
    
    // ElPosition causes
    {
        ! t[Time]: C_ElPosition(t, ElPosition(t)+1) <- Move(t) & MovingUp(t).
        ! t[Time]: C_ElPosition(t, ElPosition(t)-1) <- Move(t) & MovingDown(t).
        
        ! t[Time] dir[Direction]: Cn_ElPosition(t, ElPosition(t)) <- Move(t).
    }
    
    
    // ElDoorState inertia.
    {
        ! ds[DoorState]: ElDoorState(Start) = ds <- I_ElDoorState(ds).
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- C_ElDoorState(t, ds).
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- ElDoorState(t) = ds & ~Cn_ElDoorState(t, ds).
    }
    
    // DoorState causes
    {
        ! t[Time] f[Floor]: C_ElDoorState(t, Open) <- OpenDoors(t).
        ! t[Time] f[Floor]: C_ElDoorState(t, Closed) <- CloseDoors(t).
        
        ! t[Time] f[Floor] ds[DoorState]: Cn_ElDoorState(t, ds) <- ChangeDoorState(t).
	}   
    
    // UnansweredRequest inertia
    {
        ! f[Floor]: UnansweredRequest(Start, f) <- I_UnansweredRequest(f).
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- C_UnansweredRequest(t, f).
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- UnansweredRequest(t, f) & ~Cn_UnansweredRequest(t, f).
    }
    
    // UnansweredRequest causes
    {
        ! t[Time] f[Floor]: C_UnansweredRequest(t, f) <- MakeRequest(t, f) & ~UnansweredRequest(t, f).
        
        // UnansweredRequests are answered by closing the doors.
        ! t[Time]: Cn_UnansweredRequest(t, ElPosition(t)) <- CloseDoors(t).
    }
    
    // Direction inertia
    {
        // No initial direction.
        ! t[Time] dir[Direction]: Direction(Next(t), dir) <- C_Direction(t, dir).
        ! t[Time] dir[Direction]: Direction(Next(t), dir) <- Direction(t, dir) & ~Cn_Direction(t, dir).
    }
    
    // Direction causes
    {
    	! t[Time]: C_Direction(t, Up) <- CanChangeDirection(t) & 
        	(
        		? f1[Floor]: UnansweredRequestAbove(t, f1)
        		& (! f2[Floor]: UnansweredRequestBelow(t, f2) => f1 - ElPosition(t) =< ElPosition(t) - f2)
        	).
        ! t[Time]: C_Direction(t, Up) <- CanChangeDirection(t) & 
        	(
        		? f1[Floor]: MakeRequestAbove(t, f1)
        		& (! f2[Floor]: UnansweredRequestBelow(t, f2) => f1 - ElPosition(t) =< ElPosition(t) - f2)
        	).
        ! t[Time]: C_Direction(t, Down) <- CanChangeDirection(t) & 
        	(
        		? f1[Floor]: UnansweredRequestBelow(t, f1)
        		& (! f2[Floor]: UnansweredRequestAbove(t, f2) => ElPosition(t) - f1 < f2 - ElPosition(t))
        	).
        ! t[Time]: C_Direction(t, Down) <- CanChangeDirection(t) &
        	(
        		? f1[Floor]: MakeRequestBelow(t, f1)
        		& (! f2[Floor]: UnansweredRequestAbove(t, f2) => ElPosition(t) - f1 < f2 - ElPosition(t))
        	).
        
      	! t[Time]: Cn_Direction(t, Up) <- ~(? f[Floor]: UnansweredRequestAbove(t, f) | MakeRequestAbove(t, f)).
    	! t[Time]: Cn_Direction(t, Down) <- ~(? f[Floor]: UnansweredRequestBelow(t, f) | MakeRequestBelow(t, f)).
    }
    
    
    // CanChangeDirection definition
    {
        // Elevetor wasn't responding to requests
        ! t[Time]: CanChangeDirection(t) <- ! dir[Direction]: ~Direction(t, dir).
        // Elevator can no longer go up.
        ! t[Time]: CanChangeDirection(t) <- MovingUp(t) & Cn_Direction(t, Up).
        // Elevator can no longer go down.
        ! t[Time]: CanChangeDirection(t) <- MovingDown(t) & Cn_Direction(t, Down).
    }
    
    
    // Don't move with open doors.
    ! t[Time]: Move(t) => ElDoorState(t) = Closed.
    
    // perform an action iff there is an unanswered request
    ! t[Time]: (? f[Floor]: UnansweredRequest(t, f)) <=> (? a[Action]: do(t, a)).
    
    // Change doorstate iff the elevator is at a requested floor.
    // -> The elevator will open its doors when it arrives
    // -> The elevator will close its doors when they are open since a request is 
    //    only considered answered once the doors are closed again.
    ! t[Time]: ChangeDoorState(t) <=> UnansweredRequest(t, ElPosition(t)).
}

// If elevator moves the doors are closed!
theory move_inv : V_student {
    ! t[Time]: Move(t) => ElDoorState(t) = Closed.
}

//Doors are never open if there is no call waiting on that floor!
theory door_inv : V_student { 
    ! t[Time]: ~UnansweredRequest(t, ElPosition(t)) => ElDoorState(t) = Closed.
}

Structure S_Inv : V_student { 
    Time = {0..20}
	Floor = {0..5}
	Start = 0
}

procedure proveInvariants() {
	print(isinvariant(T_student , move_inv , S_Inv)) 
    print(isinvariant(T_student , door_inv , S_Inv))
}

// MAIN PROCEDURE
// If you have to change the main procedure for debugging purposes, be careful and restore it back once you finish debugging.
procedure main() {
    proveInvariants()
    
    local CompleteTheory = merge(T_fixed, T_student) 
    
    // Change the structure name to try other structures (check the included file "structures.idp" for available provided structures, and feel free to add additional structures) 
    model = onemodel(CompleteTheory, S7)
    // Printing model 
    print(model)

    // Visualization
    initVisualization()
    visualizeElevator(model)
}

// File responsible for visualization  
include "visualize_elevator.idp"

// Structures for testing
// You can open this file and check the given structures 
include "structures.idp"
