// First name: Bo
// Last name: Kleynen
// Student number: r0624034


// FIXED VOCABULARY
// DO NOT CHANGE THIS VOCABULARY
LTCvocabulary V_fixed {
    type Time isa nat			// A set of time points
    Start: Time 				// The initial time point
    partial Next(Time): Time 	// The successor time point of time point t
    
    type Floor isa nat									// The set of floors
    type DoorState constructed from {Closed, Open}		// A set of possible states of the elevator’s doors
	
    // Fluents
    ElPosition(Time): Floor				// The floor of the elevator at time point t
    ElDoorState(Time): DoorState		// The state of the elevator’s doors at time point t
    UnansweredRequest(Time, Floor)		// Denotes that there is an unanswered request at floor f at time point t
    
    // Actions
    MakeRequest(Time, Floor) 			// Denotes that a request is made for floor f at time point t
}


// STUDENT VOCABULARY
// Here you can add new types, functions and predicates
LTCvocabulary V_student {
    extern vocabulary V_fixed
    
    type Direction constructed from {Up, Down}
    
    type Action constructed from {Move, ChangeDoorState}
    
	do(Time, Action)
    Move(Time)
    ChangeDoorState(Time)
    CloseDoors(Time)
    
    NewDirection(Time): Direction
    
    Direction(Time, Direction)
    
    I_ElPosition(Floor)
    C_ElPosition(Time, Floor)
    Cn_ElPosition(Time, Floor)
    
    I_ElDoorState(DoorState)
    C_ElDoorState(Time, DoorState)
    Cn_ElDoorState(Time, DoorState)
    
    I_UnansweredRequest(Floor)
    C_UnansweredRequest(Time, Floor)
    Cn_UnansweredRequest(Time, Floor)
    
    I_Direction(Direction)
    C_Direction(Time, Direction)
    Cn_Direction(Time, Direction)
}

// TIME THEORY
// DO NOT CHANGE THIS THEORY
Theory T_fixed : V_student {
    // Defining time
    {
        Start = MIN[:Time].
        ! t: Next(t) = t+1 <- Time(t+1).
    }
}


// STUDENT THEORY
// Here you should model the dynamic elevator system
Theory T_student: V_student {
    // Definition of NewDirection(Time): Direction
    {
        ! t[Time]: NewDirection(t) = Up <- 
        	// There is a request for a floor above the current floor
        	? f1[Floor]: UnansweredRequest(t, f1) & f1 > ElPosition(t) & 
        	// All floors below the current floor are further away than this floor.
            (! f2[Floor]: UnansweredRequest(t, f2) & f2 < ElPosition(t) => f1 - ElPosition(t) =< ElPosition(t) - f2).
        // When the NewDirection isn't up, it must be down.
        ! t[Time]: NewDirection(t) = Down <- NewDirection(t) ~= Up.
    }
    
    // Some syntactic sugar
    ! t[Time]: Move(t) <=> do(t, Move).
    ! t[Time]: ChangeDoorState(t) <=> do(t, ChangeDoorState).
    ! t[Time]: CloseDoors(t) <=> ChangeDoorState(t) & ElDoorState(t) = Open.
    
    // In the first time step the elevator is at the bottom floor
    I_ElPosition(MIN).
    
    // In the first time step the door is closed
    I_ElDoorState(Closed).
    
    // In the first time step there can be no unanswered requests.
    ! f[Floor]: ~I_UnansweredRequest(f).
    
    // ElPosition inertia.
    {
        ! f[Floor]: ElPosition(Start) = f <- I_ElPosition(f).
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- C_ElPosition(t, f).
        ! t[Time] f[Floor]: ElPosition(Next(t)) = f <- ElPosition(t) = f & ~Cn_ElPosition(t, f).
    }
    
    // Position causes
    {
        ! t[Time]: C_ElPosition(t, ElPosition(t)+1) <- Move(t) & Direction(t, Up).
        ! t[Time]: C_ElPosition(t, ElPosition(t)-1) <- Move(t) & Direction(t, Down).
        
        ! t[Time] dir[Direction]: Cn_ElPosition(t, ElPosition(t)) <- Move(t).
    }
    
    // ElDoorState inertia.
    {
        ! ds[DoorState]: ElDoorState(Start) = ds <- I_ElDoorState(ds).
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- C_ElDoorState(t, ds).
        ! t[Time] ds[DoorState]: ElDoorState(Next(t)) = ds <- ElDoorState(t) = ds & ~Cn_ElDoorState(t, ds).
    }
    
    // DoorState causes
    {
        ! t[Time] f[Floor]: C_ElDoorState(t, Open) <- ChangeDoorState(t) & ElDoorState(t) = Closed.
        ! t[Time] f[Floor]: C_ElDoorState(t, Closed) <- ChangeDoorState(t) & ElDoorState(t) = Open.
        
        ! t[Time] f[Floor] ds[DoorState]: Cn_ElDoorState(t, ds) <- ChangeDoorState(t).
	}
    
    // UnansweredRequest inertia
    {
        ! f[Floor]: UnansweredRequest(Start, f) <- I_UnansweredRequest(f).
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- C_UnansweredRequest(t, f).
        ! t[Time] f[Floor]: UnansweredRequest(Next(t), f) <- UnansweredRequest(t, f) & ~Cn_UnansweredRequest(t, f).
    }
    
    // UnansweredRequest causes
    {
        ! t[Time] f[Floor]: C_UnansweredRequest(t, f) <- MakeRequest(t, f) & ~UnansweredRequest(t, f).
        ! t[Time]: Cn_UnansweredRequest(t, ElPosition(t)) <- ChangeDoorState(t) & ElDoorState(t) = Open.
    }
    
    // Direction inertia
    {
        ! dir[Direction]: Direction(Start, dir) <- I_Direction(dir).
        ! t[Time] dir[Direction]: Direction(Next(t), dir) <- C_Direction(t, dir).
        ! t[Time] dir[Direction]: Direction(Next(t), dir) <- Direction(t, dir) & ~Cn_Direction(t, dir).
    }
    
    // Direction causes
    {
        ! t[Time]: C_Direction(t, NewDirection(t)) <- Direction(t, Up) & ~(? f[Floor]: UnansweredRequest(t+1, f) & f > ElPosition(t)).
        ! t[Time]: C_Direction(t, NewDirection(t)) <- Direction(t, Down) & ~(? f[Floor]: UnansweredRequest(t+1, f) & f < ElPosition(t)).
        
      ! t[Time]: Cn_Direction(t, Up) <- ~(? f[Floor]: f > ElPosition(t) & UnansweredRequest(t+1, f)).
      ! t[Time]: Cn_Direction(t, Down) <- ~(? f[Floor]: f < ElPosition(t) & UnansweredRequest(t+1, f)).
    }
    
    // PRECONDITIONS.
    
    // Don't move with open doors.
    ! t[Time]: Move(t) => ElDoorState(t) = Closed.
    
    // perform an action iff there is an unanswered request
    ! t[Time]: (? f[Floor]: UnansweredRequest(t, f)) <=> (? a[Action]: do(t, a)).
    
    // Change doorstate iff the elevator is at a requested floor.
    // -> The elevator will open its doors when it arrives
    // -> The elevator will close its doors when they are open since a request is 
    //    only considered answered once the doors are closed again.
    ! t[Time]: ChangeDoorState(t) <=> UnansweredRequest(t, ElPosition(t)).
}

// MAIN PROCEDURE
// If you have to change the main procedure for debugging purposes, be careful and restore it back once you finish debugging.
procedure main() {
    local CompleteTheory = merge(T_fixed, T_student) 
    
    // Change the structure name to try other structures (check the included file "structures.idp" for available provided structures, and feel free to add additional structures)                                
    model = onemodel(CompleteTheory, S5)
     
    
    // Printing model 
    print(model)

    // Visualization
    initVisualization()
    visualizeElevator(model)
}

// File responsible for visualization  
include "visualize_elevator.idp"

// Structures for testing
// You can open this file and check the given structures 
include "structures.idp"
